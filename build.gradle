plugins {
    id 'jacoco'
    id 'com.github.spotbugs' version '5.0.5'
    id 'pmd'
    id 'checkstyle'
    id 'java-gradle-plugin'
    id 'maven-publish'

    // This is for local testing.
    // It's a configuration for consumers.
    // Please add this after you published your plugin to the local directory.
    //id 'com.levelrin.github-pr-checker' version '0.1.0'
}

group 'com.levelrin'
version '0.1.0'

sourceCompatibility = 1.15

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation 'com.jayway.jsonpath:json-path:2.7.0'
    implementation 'com.github.spotbugs:spotbugs-annotations:4.5.3'
    implementation 'org.jetbrains:annotations:22.0.0'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.0.0.202111291000-r'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:4.3.1'
}

test {
    useJUnitPlatform()
    dependsOn 'cleanTest'
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat = 'full'
    }
}

jacoco {
    toolVersion = '0.8.7'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                enabled = true
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.66617
            }
            limit {
                enabled = true
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.75000
            }
            limit {
                enabled = true
                counter = 'COMPLEXITY'
                value = 'COVEREDRATIO'
                minimum = 0.73684
            }
            limit {
                enabled = true
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.65317
            }
            limit {
                enabled = true
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.78125
            }
            limit {
                enabled = true
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 0.90000
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

check.dependsOn jacocoTestCoverageVerification
check.dependsOn jacocoTestReport

spotbugs {
    toolVersion = '4.5.3'
    ignoreFailures = false
    effort = 'max'
    excludeFilter = file('config/spotbugs/exclude.xml')
}
spotbugsMain {
    reports {
        html {
            required = true
            outputLocation  = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}
spotbugsTest {
    reports {
        html {
            required = true
            outputLocation  = file("$buildDir/reports/spotbugs/test/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

pmd {
    toolVersion = '6.31.0'
    ignoreFailures = false
    ruleSetFiles = files('config/pmd/ruleSet.xml')
    ruleSets = []
}
tasks.withType(Pmd) {
    reports {
        xml.required = true
        html.required = true
    }
}

checkstyle {
    toolVersion '8.41'
    ignoreFailures = false
    configFile = file('config/checkstyle/checkstyle.xml')
    configProperties = [
        'checkstyle.header.file': file('config/checkstyle/java.header'),
        'checkstyle.suppression.file': file('config/checkstyle/suppression.xml')
    ]
}
tasks.withType(Checkstyle) {
    reports {
        xml.required = true
        html.required = true
    }
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'com.levelrin.github-pr-checker'
            implementationClass = 'com.levelrin.gradle.github.pr.checker.GitHubPrPlugin'
        }
    }
}

// This is for local testing.
// It's a configuration for producers.
// The 'publish' task will generate the consumable plugin files in the specified directory.
// You can add the specified directory to the Gradle repository.
// And then, you can add the plugin id to test this plugin.
/*publishing {
    repositories {
        maven {
            url = "$buildDir/repo"
        }
    }
}*/

// This is for local testing.
// It's a configuration for consumers.
// Please generate the plugin files before adding this configuration.
/*githubPr {
    owner = 'flutter'
    repo = 'flutter'
    token = System.getenv('GITHUB_TOKEN')
    outputDir = file "$buildDir/github-pr-checker"
}*/
